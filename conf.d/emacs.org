#+PROPERTY: header-args:emacs-lisp :tangle yes
* About
This is my literate configuration for [[https://www.gnu.org/software/emacs/][Emacs]].
This document includes a collection of configuration snippets to express how I personally like to use Emacs, each accompanied by some reasoning.
I think it's important to include reasoning for each part so I can understand /why/ I use it. Plus the added benefit of others being able to peruse and borrow parts, just as I have from others.

The combination of literacy and functionality is achieved using the amazing [[http://orgmode.org/][org-mode]], with [[http://orgmode.org/worg/org-contrib/babel/][org-babel]].

Throughout this document, you'll notice heavy use of the brilliant [[https://github.com/jwiegley/use-package][use-package]].
For anyone who hasn't tried out =use-package=; I emplore you to do so - it truly makes managing your configuration an absolute joy.

* Configuration
** General
*** About me
Tell Emacs who's driving
#+BEGIN_SRC emacs-lisp
(setq user-full-name    "Calum MacRae"
      user-mail-address "hi@cmacr.ae")
#+END_SRC

*** Global =:ensure= & =:defer= for use-package statements
=use-package= has an =:ensure= keyword which dictates whether packages are installed or not.
As most of my =use-package= configurations are for external packages, I set this to always ensure.
Then, in cases where I don't want this to be true, I simply set =:ensure nil=.
For any nested statements, it's necessary to set =:ensure t=.
#+BEGIN_SRC emacs-lisp
(setq use-package-always-ensure t)
#+END_SRC

*** Start the server
Start the Emacs server so other clients can connect and use the same session.
This is useful for when you may be oprating Emacs from the GUI usually, but want to use the same session from a TTY/terminal.
Also handy for when you have your ~EDITOR~ set to ~emacsclient~.
#+BEGIN_SRC emacs-lisp
(server-start)
#+END_SRC

*** A few Darwin specific configurations
To make Emacs play a little nicer with window management, enable menu-bar-mode.
Also, set the frame's dimensions based on pixels - this makes Emacs play nicer with tiling
window managers, where no title bar is displayed.
Also, when typing =M-3=, insert an octothorp - I use a British keyboard.
#+BEGIN_SRC emacs-lisp
(cond
 ((string-equal system-type "darwin")
  (setq frame-resize-pixelwise  t
        mac-option-modifier     'meta)

  (define-key key-translation-map (kbd "M-3") (kbd "#"))
  (menu-bar-mode t)))
#+END_SRC

*** Deactivation of functionality I don't tend to use
- Backup files
- Autosaving
- Start-up message
- Audible bell
- Toolbar, tooltip, scrollbar, blinking cursor
#+BEGIN_SRC emacs-lisp
(setq make-backup-files       nil
      auto-save-default       nil
      inhibit-startup-message t
      ring-bell-function      'ignore)
(dolist (mode
         '(tool-bar-mode
           tooltip-mode
           scroll-bar-mode
           blink-cursor-mode))
  (funcall mode 0))
#+END_SRC

*** Comfier frame settings
#+BEGIN_SRC emacs-lisp
(setq default-frame-alist
      (append (list
               '(min-height . 1)
               '(height     . 45)
               '(min-width  . 1)
               '(width      . 81)
               '(vertical-scroll-bars . nil)
               '(internal-border-width . 10)
               '(left-fringe    . 0)
               '(right-fringe   . 0)
               '(tool-bar-lines . 0)
               '(menu-bar-lines . 0))))
#+END_SRC

*** Set =fill-column=
#+BEGIN_SRC emacs-lisp
(setq-default fill-column 80)
#+END_SRC

*** winner-mode
#+BEGIN_SRC emacs-lisp
(winner-mode 1)
#+END_SRC

*** Globally highlight the current line
#+BEGIN_SRC emacs-lisp
(global-hl-line-mode)
#+END_SRC

*** Discard customizations
Emacs has a comprehensive customization system that allows configuration changes interactively.
Personally, I opt to ensure all the configuration I use for my environment is declarative.
As such, the following configuration sets the ~custom-file~ to be a random temporary file created each time Emacs starts.
This means any customizations made interactively are discarded entirely.
#+BEGIN_SRC emacs-lisp
(setq custom-file (make-temp-file ""))
#+END_SRC

*** Just use 'y' or 'n' instead of 'yes' or 'no'
You'll find =yes-or-no= prompts coming up in Emacs a lot.
I'd much rather just type =y= or =n= than =yes= or =no= every time...
#+BEGIN_SRC emacs-lisp
(fset 'yes-or-no-p 'y-or-n-p)
#+END_SRC

*** Confirm quit
This adds a confirmation prompt when quitting Emacs - because I'm only human.
#+BEGIN_SRC emacs-lisp
(setq confirm-kill-emacs 'yes-or-no-p)
#+END_SRC

*** Set the scratch buffer string
Set the scratch buffer's initial contents to include a comment with a timestamp of creation.
Not really all that useful, but cleaner than the default comment, and I like having something there.
#+BEGIN_SRC emacs-lisp
(setq initial-scratch-message (format ";; Scratch buffer - started on %s\n\n" (current-time-string)))
#+END_SRC

*** Configure FlySpell to use aspell
I use =aspell=, so this simply sets [[https://www.emacswiki.org/emacs/FlySpell][Flyspell]] to use it and passes a couple extra arguments
#+BEGIN_SRC emacs-lisp
(setq ispell-program-name "aspell"
      ispell-extra-args   '("--sug-mode=ultra" "--lang=en_GB"))
#+END_SRC

*** Calendar/Diary
Set the start of the week for the calendar to be Monday.
Sort entries when viewing diary items.
#+BEGIN_SRC emacs-lisp
(setq calendar-week-start-day 1
      diary-file              "~/org/diary")
(add-hook 'diary-list-entries-hook 'diary-sort-entries t)
#+END_SRC

*** IRC
Emacs comes with a great builtin IRC client: ERC.
These are some general settings that're all pretty self explanatory: hide particular activity, autojoin channels for some servers.
For convenience, I've also defined a =erc-conn= function for my usual connection parameters.
#+BEGIN_SRC emacs-lisp
(use-package erc
  :ensure nil
  :custom
  (erc-server "irc.freenode.net" )
  (erc-nick "cmacrae")
  (erc-hide-list '("PART" "QUIT" "JOIN"))
  (erc-autojoin-channels-alist '(("freenode.net"
                                  "#lobsters"
                                  "#nixos"
                                  "#nix-darwin")))
  :config
  (defun cm/erc-conn ()
    (interactive)
    (erc-tls :server "irc.freenode.net" :port 6697 :nick "cmacrae")))
#+END_SRC

*** org-mode
Various pieces of configuration for the mighty org-mode.
#+BEGIN_SRC emacs-lisp
  (use-package org-mode
    :ensure nil
    :hook
    (org-mode . org-indent-mode)
    (org-mode . variable-pitch-mode)
    (org-mode . visual-line-mode)
    :custom-face
    (avy-lead             ((t (:inherit fixed-pitch))))
    (avy-lead-face-0      ((t (:inherit fixed-pitch))))
    (avy-lead-face-1      ((t (:inherit fixed-pitch))))
    (avy-lead-face-2      ((t (:inherit fixed-pitch))))
    (company-tooltip      ((t (:inherit fixed-pitch))))
    (org-table            ((t (:inherit fixed-pitch))))
    (org-formula          ((t (:inherit fixed-pitch))))
    (org-checkbox         ((t (:inherit fixed-pitch))))
    (org-code             ((t (:inherit fixed-pitch))))
    (org-verbatim         ((t (:inherit fixed-pitch))))
    (org-special-keyword  ((t (:inherit fixed-pitch))))
    (org-meta-line        ((t (:inherit fixed-pitch))))
    (org-block            ((t (:foreground nil :inherit fixed-pitch))))
    (org-indent           ((t (:inherit (org-hide fixed-pitch)))))
    (fixed-pitch          ((t (:family "Menlo" :height 120))))
    (variable-pitch       ((t (:family "ETBembo" :height 160))))

    :init
    (setq org-ellipsis " ▾"
	  org-hide-emphasis-markers t
	  org-src-fontify-natively t
	  org-fontify-quote-and-verse-blocks t
	  org-src-tab-acts-natively t
	  org-edit-src-content-indentation 0 
	  org-src-preserve-indentation t
	  org-cycle-separator-lines 2)
  
    (font-lock-add-keywords 'org-mode
                            '(("^ *\\([-]\\) "
                               (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "•"))))))
  
    :config
    (evil-define-key '(normal insert visual) org-mode-map (kbd "M-k") 'org-metaup)
    (evil-define-key '(normal insert visual) org-mode-map (kbd "M-j") 'org-metadown)
    (evil-define-key '(normal insert visual) org-mode-map (kbd "C-j") 'org-next-visible-heading)
    (evil-define-key '(normal insert visual) org-mode-map (kbd "C-k") 'org-previous-visible-heading)
  
    (dolist (face '((org-level-1 . 1.2)
                    (org-level-2 . 1.1)
                    (org-level-3 . 1.05)
                    (org-level-4 . 1.0)
                    (org-level-5 . 1.1)
                    (org-level-6 . 1.1)
                    (org-level-7 . 1.1)
                    (org-level-8 . 1.1)))))
   
  (use-package org-superstar
    :hook (org-mode . org-superstar-mode)
    :custom
    (org-superstar-remove-leading-stars t)
    (org-superstar-headline-bullets-list '("◉" "○" "●" "○" "●" "○" "●")))

  (use-package org-appear
    :hook (org-mode . org-appear-mode))
#+END_SRC

*** Native compilation
I'm using the native compilation features introduced in Emacs 28.
The following setting suppresses automatic display of warning messages.
#+BEGIN_SRC emacs-lisp
(setq comp-async-report-warnings-errors        nil
      native-comp-async-report-warnings-errors nil)
#+END_SRC

** Meta Configurations
This section includes the use of any "meta" collection of Emacs configurations provided by others.
I'm using [[https://github.com/rougier/nano-emacs/issues/37][rougier/nano-emacs]] as a base. The configuration is shaped around principles that align with my own.

Currently I'm making some changes locally that I intend to send for review upstream, so I'm loading from a local
path.

Any customisations that I'd deem too specific to my preference I'll explicitly outline here.
- Override the modeline for term/vterm mode to show current directory & =kubectl= context and namespace (if not =default=)
- Override the modeline for mu4e's main & header views to inject some fancy icons

  #+BEGIN_SRC emacs-lisp
(use-package ts)
(use-package nano-theme
  :ensure nil
  :demand t
  :load-path "~/src/github.com/cmacrae/nano-theme"
  :config
  (load-theme 'nano t))
 
(use-package nano-modeline
  :ensure nil
  :load-path "~/src/github.com/cmacrae/nano-modeline"
  :hook (after-init . nano-modeline))

;; (use-package nano-emacs
;;   :ensure nil
;;   :load-path "~/src/github.com/cmacrae/nano-emacs"
;;   :init
;;   ;; (require 'nano-modeline)
;;   (require 'nano-writer)
;;   ;; (require 'nano-layout)
;;   (require 'nano-mu4e))

;;   ;; (defun cm/nano-modeline-term-mode ()
;;   ;;   "Modline for term/vterm mode with current project (or directory) and kubectl context/namespace."
;;   ;;   (nano-modeline-compose " >_ "
;;   ;;                         "Terminal"
;;   ;; 			     (concat "| "
;;   ;; 			       (if (fboundp 'projectile-default-project-name)
;;   ;; 			             (projectile-project-name)
;;   ;; 			           (substring (shorten-directory default-directory 32) 0 -1)))
;;   ;;                         (let
;;   ;; 			     ((ns (shell-command-to-string "kubectl config view --minify --output 'jsonpath={..namespace}'"))
;;   ;; 			      (context (substring (shell-command-to-string "kubectl config current-context") 0 -1)))
;;   ;; 			       (if (string= "default" ns)
;;   ;; 			         (format "%s" context)
;;   ;; 			         (format "%s:%s" context ns)))))

;;   ;; (defun cm/nano-modeline-mu4e-main-mode ()
;;   ;;   "Modeline for mu4e's main view with a fancy mail icon"
;;   ;;   (nano-modeline-compose (if (fboundp 'all-the-icons-faicon)
;;   ;; 			       (concat " " (all-the-icons-faicon
;;   ;; 			        "envelope"
;;   ;; 			        :height 5
;;   ;; 			        :face 'nano-face-header-faded
;;   ;; 			        :v-adjust -0.1) " ")
;;   ;; 			       (nano-modeline-status))
;;   ;;                          "Mail"
;;   ;;                          (nano-modeline-mu4e-context)
;;   ;;                          (format-time-string "%A %d %B %Y, %H:%M")))


;;   ;; (defun cm/nano-modeline-mu4e-headers-mode ()
;;   ;;  "Modeline for mu4e's headers view with a fancy search icon"
;;   ;;   (nano-modeline-compose (if (fboundp 'all-the-icons-faicon)
;;   ;;                           (concat " " (all-the-icons-faicon
;;   ;;                            "search"
;;   ;;                            :height 5
;;   ;;                            :face 'nano-face-header-faded
;;   ;;                            :v-adjust -0.1) " ")
;;   ;;                            (nano-modeline-status))
;;   ;;                           (mu4e~quote-for-modeline mu4e~headers-last-query)
;;   ;;                          ""
;;   ;;                          ""))

;;   ;; (defun cm/nano-modeline-mu4e-dashboard-mode ()
;;   ;;  "Modeline for mu4e-dashboard with a fancy mail icon"
;;   ;;   (nano-modeline-compose (if (fboundp 'all-the-icons-faicon)
;;   ;; 			       (concat " " (all-the-icons-faicon
;;   ;; 			        "envelope"
;;   ;; 			        :height 5
;;   ;; 			        :face 'nano-face-header-faded
;;   ;; 			        :v-adjust -0.1) " ")
;;   ;; 			       (nano-modeline-status))
;;   ;;                          "Mail"
;;   ;;                          (nano-modeline-mu4e-context)
;;   ;;                          ""))

;;   ;; (advice-add #'nano-modeline-term-mode :override #'cm/nano-modeline-term-mode)
;;   ;; (advice-add #'nano-modeline-mu4e-main-mode :override #'cm/nano-modeline-mu4e-main-mode)
;;   ;; (advice-add #'nano-modeline-mu4e-headers-mode :override #'cm/nano-modeline-mu4e-headers-mode)
;;   ;; (advice-add #'nano-modeline-mu4e-dashboard-mode :override #'cm/nano-modeline-mu4e-dashboard-mode)

;;   :hook
;;   (after-init . nano-faces)
;;   (after-init . nano-theme))
  #+END_SRC

** Packages
*** use-package
I use [[https://github.com/nix-community/emacs-overlay][nix-community/emacs-overlay]]'s =emacsWithPackagesFromUsePackage= function to manage my package installation with Nix, but still
control it from within this file. Whilst this statement may seem recursive, it ensures =use-package= is installed before it is ever loaded.
#+BEGIN_SRC emacs-lisp
(use-package use-package)
#+END_SRC

*** Vertico | savehist | corfu | orderless | marginalia | Embark
This section outlines my preference for Emacs' completion system/interface.
- Vertico: consistent, minimalistic completion UI
- savehist: persists completion history
- corfu: enhanced =completion-in-region= functionality, mainly for extension of =completion-at-point= & Capfs based tooling (including LSP)
- orderless: intuitive completion style for candidates based on space separated patterns
- marginalia: annotations for minibuffer candidates
- Embark: act on targets, sort of like a right-click menu
#+BEGIN_SRC emacs-lisp
(use-package vertico
  :hook (after-init . vertico-mode)

  :custom
  (vertico-resize t))
 
(use-package savehist-mode
  :ensure nil
  :hook (after-init . savehist-mode))
 
(use-package corfu
  :hook (after-init . corfu-global-mode))

;; TODO [orderless] failing to be found in Nix's ELPA/MELPA repositories
;; (use-package orderless
;;   :hook after-init
;;   :custom
;;   (completion-styles '(orderless)
;;   (completion-category-defaults nil)
;;   (completion-category-overrides '((file (styles partial-completion))))

;; TODO: [marginalia] byte-compilation issue
;; (use-package marginalia
;;   :hook (after-init .marginalia-mode)
;;   :custom
;;   (marginalia-annotators '(marginalia-annotators-heavy marginalia-annotators-light nil)))

(use-package embark
  :bind
  (("C-," . embark-act))

  :init
  (setq prefix-help-command #'embark-prefix-help-command))
#+END_SRC

*** which-key
Helpful reminders for which keybindings are available
#+BEGIN_SRC emacs-lisp
(use-package which-key
  :hook (after-init . which-key-mode))
#+END_SRC

*** Evil
Vim emulation in Emacs. Because: yes, you can have the best of both worlds!
Below you'll find various extensions to my Evil layer that generally improve the quality of life.
#+BEGIN_SRC emacs-lisp
(use-package evil
  :init
  (setq evil-want-C-u-scroll t)
  (setq evil-want-keybinding nil)
  :hook (after-init . evil-mode))
#+END_SRC

**** Evil Collection
A collection of Evil bindings, for the parts of Emacs that Evil does not cover properly by default
#+BEGIN_SRC emacs-lisp
(use-package evil-collection
  :after evil
  :hook (evil-mode . evil-collection-init))
#+END_SRC

**** EasyMotion
Buffer traversal made easy! Emulates easymotion.vim
#+BEGIN_SRC emacs-lisp
(use-package evil-easymotion
  :after evil
  :config
  (evilem-default-keybindings "SPC"))
#+END_SRC

**** Goggles
Visual hints when performing Evil operations (dd, yy, cw, p, etc.)
#+BEGIN_SRC emacs-lisp
(use-package evil-goggles
  :after evil
  :hook (evil-mode . evil-goggles-mode)
  :config (evil-goggles-use-diff-faces))
#+END_SRC

**** Lion
Align operators (gl & gL), emulating lion.vim
#+BEGIN_SRC emacs-lisp
(use-package evil-lion
  :after evil
  :hook (evil-mode . evil-lion-mode))
#+END_SRC

**** Commentary
Easily comment lines/blocks. Emulates commentary.vim
#+BEGIN_SRC emacs-lisp
(use-package evil-commentary
  :after evil
  :hook (evil-mode . evil-commentary-mode))
#+END_SRC

**** Snipe
2-char searching with f, F, t, T operators. Like seek.vim/sneak.vim
#+BEGIN_SRC emacs-lisp
(use-package evil-snipe
  :after evil
  :hook
  (evil-mode . evil-snipe-mode)
  (evil-snipe-mode . evil-snipe-override-mode))
#+END_SRC

**** multiedit
#+BEGIN_SRC emacs-lisp
(use-package evil-multiedit
  :after evil
  :hook (evil-mode . evil-multiedit-default-keybinds)
  :config
  (evil-ex-define-cmd "ie[dit]" 'evil-multiedit-ex-match))
#+END_SRC

**** surround
#+BEGIN_SRC emacs-lisp
(use-package evil-surround
  :after evil
  :hook (evil-mode . global-evil-surround-mode))
#+END_SRC

*** mu4e
Email! mu4e comes bundled as part of the =mu= package, so I load it from my Nix profile.
#+BEGIN_SRC emacs-lisp
(use-package mu4e
  :ensure nil
  :load-path "~/.nix-profile/share/emacs/site-lisp/mu4e"

  :config
  (use-package mu4e-thread-folding
    :ensure nil
    :after evil
    :hook (mu4e-headers-mode . mu4e-thread-folding-mode)
    :config
    (add-to-list 'mu4e-header-info-custom
                 '(:empty . (:name "Empty"
                                   :shortname ""
                                   :function (lambda (msg) "  "))))
    (setq mu4e-headers-fields '((:empty         .    2)
                                (:human-date    .   12)
                                (:flags         .    6)
                                (:mailing-list  .   10)
                                (:from          .   22)
                                (:subject       .   nil)))
    (evil-define-key 'normal mu4e-headers-mode-map (kbd "<S-h>") 'mu4e-headers-fold-all)
    (evil-define-key 'normal mu4e-headers-mode-map (kbd "<S-l>") 'mu4e-headers-unfold-all)
    (evil-define-key 'normal mu4e-headers-mode-map (kbd "h")     'mu4e-headers-fold-at-point)
    (evil-define-key 'normal mu4e-headers-mode-map (kbd "l")     'mu4e-headers-unfold-at-point)
    (evil-define-key 'normal mu4e-headers-mode-map (kbd "<tab>") 'mu4e-headers-toggle-at-point))

  (use-package svg-tag-mode)

  (use-package mu4e-dashboard
    :ensure nil
    ;; :load-path "~/src/github.com/cmacrae/mu4e-dashboard"
    :custom (mu4e-dashboard-file "~/org/mail-sidebar.org"))
  ;; :bind
  ;; (:map mu4e-main-mode-map
  ;;   ("C-;"     . mu4e-dashboard-show))
  ;; (:map mu4e-headers-mode-map
  ;;   ("C-;"     . mu4e-dashboard-show)
  ;;   ("C-c C-;" . mu4e-dasboard-toggle-window))
  ;; (:map mu4e-dashboard-mode-map
  ;;   ("C-;"     . (lambda () (interactive) (select-window (previous-window))))))

  (defun my-mu4e-action-view-with-xwidget (msg)
    "View the body of the message inside xwidget-webkit."
    (unless (fboundp 'xwidget-webkit-browse-url)
      (mu4e-error "No xwidget support available"))
    (let* ((html (mu4e-message-field msg :body-html))
           (txt (mu4e-message-field msg :body-txt))
           (tmpfile (format "%s%x.html" temporary-file-directory (random t))))
      (unless (or html txt)
        (mu4e-error "No body part for this message"))
      (with-temp-buffer
        (insert (or html (concat "<pre>" txt "</pre>")))
        (write-file tmpfile)
        (xwidget-webkit-browse-url (concat "file://" tmpfile) t))))

  (add-to-list 'mu4e-view-actions '("xViewXWidget" . my-mu4e-action-view-with-xwidget) t)
  (add-to-list 'mu4e-view-actions '("bViewInBrowser" . mu4e-action-view-in-browser) t)

  (setq mu4e-index-update-in-background      nil
        mu4e-change-filenames-when-moving    t
        mu4e-headers-date-format             "%a %d/%m/%y"
        sendmail-program                     "msmtp"
        message-sendmail-f-is-evil           t
        send-mail-function                   'smtpmail-send-it
        message-sendmail-extra-arguments     '("--read-envelope-from")
        message-send-mail-function           'message-send-mail-with-sendmail
        mml-secure-openpgp-sign-with-sender  t
        mu4e-fold-threads-in-search-results  t)

  (setq mu4e-contexts `(
                        ,(make-mu4e-context
                          :name "Fastmail"
                          :enter-func (lambda ()
                                        (add-hook 'message-send-hook 'mml-secure-message-sign-pgpmime)
                                        (mu4e-message "Switch to the Fastmail context"))
                          :leave-func (lambda ()
                                        (remove-hook 'message-send-hook 'mml-secure-message-sign-pgpmime))
                          :match-func (lambda (msg)
                                        (when msg
                                          (string-match-p "^/fastmail" (mu4e-message-field msg :maildir))))

                          :vars '( ( user-mail-address       . "hi@cmacr.ae" )
                                   ( mml-secure-openpgp-sign-with-sender . t )
                                   ( mu4e-compose-signature . "Calum MacRae\n" )
                                   ( mu4e-sent-folder   . "/fastmail/Sent"   )
                                   ( mu4e-drafts-folder . "/fastmail/Drafts" )
                                   ( mu4e-maildir-shortcuts . ( ("/fastmail/Inbox"   . ?i)
                                                                ("/fastmail/Sent"    . ?s)
                                                                ("/fastmail/Archive" . ?a) ))))

                        ,(make-mu4e-context
                          :name "Work"
                          :enter-func (lambda () (mu4e-message "Switch to the Work context"))
                          :match-func (lambda (msg)
                                        (when msg
                                          (string-match-p "^/work" (mu4e-message-field msg :maildir))))
                          :vars '( ( user-mail-address       . "calum.macrae@nutmeg.com" )
                                   ( mu4e-compose-signature  .
                                     (concat
                                      "Calum MacRae\n"
                                      "DevOps\n"
                                      "Engineering Efficiency\n") )
                                   ( mu4e-sent-folder   . "/work/Sent"   )
                                   ( mu4e-drafts-folder . "/work/Drafts" )
                                   ( mu4e-maildir-shortcuts . ( ("/work/Inbox"   . ?i)
                                                                ("/work/Sent"    . ?s)
                                                                ("/work/Archive" . ?a) )))))))
#+END_SRC

*** Projectile
Project management based on version control repositories.
Absolutely essential package for me. This makes hopping around and between various projects really easy.
Not only that, but it allows project-wide actions. Like killing all buffers for a project, performing a project-wide find-and-replace, or a grep, etc.

Some configuration I use:
- Setting the completion system to =ivy=
- Further integration of Counsel with Projectile than what's provided natively
#+BEGIN_SRC emacs-lisp
(use-package projectile
  :hook
  (after-init . projectile-global-mode)
  :custom
  ;; (projectile-completion-system 'ivy)
  (projectile-switch-project-action 'neotree-projectile-action)
  :config
  (use-package counsel-projectile
    :ensure t
    :after projectile
    :hook
    (projectile-global-mode . counsel-projectile-mode)
    :bind
    ("C-c p s r" . counsel-projectile-rg)
    (:map projectile-mode-map
          ("C-c p p" . projectile-persp-switch-project)
          ("C-c p f" . counsel-projectile-find-file))))
#+END_SRC

*** perspective
Workspaces! Indespensible if you work on a lot of projects. Perspective is like workspaces (virtual desktops) for Emacs. It’s a means of namespacing a group of tangible buffers. When combined with Projectile, this becomes a really nice combination as projects then seemlessly translate to workspaces.
Here, I’ve defined a cm/persp-neo function for use with persp-switch-hook. This makes NeoTree follow the perspective when switching. I’ve also added a hydra for various Perspective actions.

#+BEGIN_SRC emacs-lisp
(use-package perspective
  :hook (after-init . persp-mode)
  :custom (persp-show-modestring nil)
  :config

  (use-package persp-projectile
    :ensure t
    :after perspective
    :hook (persp-switch . cm/persp-neo)
    :bind ("C-c x" . hydra-persp/body)

    :config
    (defun cm/persp-neo ()
      "Make NeoTree follow the perspective"
      (interactive)
      (let ((cw (selected-window))
            (path (buffer-file-name))) ;; save current window and buffer
        (progn
          (when (and (fboundp 'projectile-project-p)
                     (projectile-project-p)
                     (fboundp 'projectile-project-root))
            (neotree-dir (projectile-project-root)))
          (neotree-find path))
        (select-window cw)))

    (defhydra hydra-persp (:columns 4
                                    :color blue)
      "Perspective"
      ("a" persp-add-buffer "Add Buffer")
      ("i" persp-import "Import")
      ("c" persp-kill "Close")
      ("n" persp-next "Next")
      ("p" persp-prev "Prev")
      ("k" persp-remove-buffer "Kill Buffer")
      ("r" persp-rename "Rename")
      ("A" persp-set-buffer "Set Buffer")
      ("s" persp-switch "Switch")
      ("C-x" persp-switch-last "Switch Last")
      ("b" persp-switch-to-buffer "Switch to Buffer")
      ("P" projectile-persp-switch-project "Switch Project")
      ("q" nil "Quit"))))
#+END_SRC

*** Neotree
Sidebar filebrowser, very handy. People seem to have accepted Treemacs as the new norm, but I like NeoTree :) Here, I’ve defined some key mappings that make it a little nicer to interact with - they should be quite self-explanatory.

#+BEGIN_SRC emacs-lisp
(use-package neotree
  :after evil
  :hook (neotree-mode . (lambda () (setq mode-line-format nil)))
  :bind
  ("C-;"     . neotree-show)
  ("C-c C-;" . neotree-toggle)
  (:map neotree-mode-map
        ("C-c C-h" . neotree-hidden-file-toggle)
        ("C-c C-y" . neotree-copy-filepath-to-yank-ring)
        ("C-;"     . (lambda () (interactive) (select-window (previous-window)))))
  :custom
  (neo-theme (if window-system 'icons 'arrows))
  :config
  (evil-define-key 'normal neotree-mode-map (kbd "TAB") 'neotree-enter)
  (evil-define-key 'normal neotree-mode-map (kbd "SPC") 'neotree-quick-look)
  (evil-define-key 'normal neotree-mode-map (kbd "q") 'neotree-hide)
  (evil-define-key 'normal neotree-mode-map (kbd "RET") 'neotree-enter))
#+END_SRC

*** Magit
The one true Git porcelain!
Truely a joy to use - it surfaces the power of Git in such a fluent manner.
Anyone using Git and Emacs *needs* Magit in their life!
#+BEGIN_SRC emacs-lisp
(use-package magit
  :bind ("C-c m" . magit-status))

(use-package forge
  :after magit)

(use-package magit-todos
  :after magit)
#+END_SRC

*** git-link
Quickly yank a Git forge link for the current file/line.
Useful for when collaborating and you want to share what you're looking at.
#+BEGIN_SRC emacs-lisp
(use-package git-link
  :bind
  ("C-c g l" . git-link))
#+END_SRC

*** vterm
Fully-fledged terminal emulator based on [[https://github.com/neovim/libvterm][libvterm]]!
I manage the module and elisp as a Nix overlay in [[https://github.com/cmacrae/config][my system configuration]], so no need to install it.
Set it up to play nice with Evil.

#+BEGIN_SRC emacs-lisp
(use-package vterm
  :after evil
  :hook
  (vterm-mode . (lambda ()
                  (setq-local evil-insert-state-cursor 'hbar)
                  (evil-insert-state)))
  :config
  (define-key vterm-mode-map [return]                      #'vterm-send-return)
  (setq vterm-keymap-exceptions nil)
  (evil-define-key 'insert vterm-mode-map (kbd "C-e")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-f")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-a")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-v")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-b")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-w")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-u")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-d")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-n")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-m")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-p")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-j")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-k")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-r")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-t")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-g")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-c")      #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-SPC")    #'vterm--self-insert)
  (evil-define-key 'insert vterm-mode-map (kbd "C-y")      #'vterm--self-insert)
  (evil-define-key 'normal vterm-mode-map (kbd "C-d")      #'vterm--self-insert)
  (evil-define-key 'normal vterm-mode-map (kbd "p")        #'vterm-yank)
  (evil-define-key 'normal vterm-mode-map (kbd "i")        #'evil-insert-resume)
  (evil-define-key 'normal vterm-mode-map (kbd "o")        #'evil-insert-resume)
  (evil-define-key 'normal vterm-mode-map (kbd "<return>") #'evil-insert-resume)

  (setq vterm-ignore-blink-cursor t)

  (use-package multi-vterm
    :ensure t
    :bind
    ("C-c p t" . multi-vterm-project)))
#+END_SRC

*** Flycheck
Have Flycheck turned on for everything - checking stuff is always good!
#+BEGIN_SRC emacs-lisp
(use-package flycheck
  :hook (after-init . global-flycheck-mode))
#+END_SRC

*** Company
Slick auto-complete framework
#+BEGIN_SRC emacs-lisp
(use-package company
  :hook (after-init . global-company-mode)
  :custom
  (company-idle-delay 0)
  (company-minimum-prefix-length 1))
#+END_SRC

*** hydra
Great package to tie tangible actions together into convenient keybinding landscapes.
Here, you'll find some "general" hydras - other hydras that are centric around packages will be found with that package's configuration.

General hydras:
- Zoom: increase/decrease current buffer text size
- Transpose: transpose various constructs of text
- Toggle mode: turn frequently "toggled" modes on and off

#+BEGIN_SRC emacs-lisp
(use-package hydra
  :bind
  ("C-c z" . hydra-zoom/body)
  ("C-c T" . hydra-transpose/body)

  :config
  ;; Zoom
  (defhydra hydra-zoom ()
    "Zoom"
    ("i" text-scale-increase "In")
    ("o" text-scale-decrease "Out")
    ("q" nil "Quit" :color blue))

  ;; Transpose
  (defhydra hydra-transpose (:color red)
    "Transpose"
    ("c" transpose-chars "Characters")
    ("w" transpose-words "Words")
    ("l" transpose-lines "Lines")
    ("s" transpose-sentences "Sentences")
    ("p" transpose-paragraphs "Paragraphs")
    ("q" nil "Quit" :color blue)))
#+END_SRC

*** ace-window
Jump around Emacs windows & frames using character prefixes.
I use this constantly - it even works across multiple frames.
Also added a hydra borrowed from [[https://oremacs.com/2015/01/29/more-hydra-goodness/][here]] for some really convenient movement/manipulation!
#+BEGIN_SRC emacs-lisp
(use-package ace-window
  :bind ("M-o" . hydra-window/body)
  :custom
  (aw-dispatch-always t)
  (aw-keys '(?a ?s ?d ?f ?g ?h ?j ?k ?l))
  :config
  (defhydra hydra-window (:color blue)
    "window"
    ("h" windmove-left "left")
    ("j" windmove-down "down")
    ("k" windmove-up "up")
    ("l" windmove-right "right")
    ("a" ace-window "ace")
    ("s" (lambda () (interactive) (ace-window 4)) "swap")
    ("d" (lambda () (interactive) (ace-window 16)) "delete")
    ("q" nil "Quit")))
#+END_SRC

*** password-store
I use [[https://www.passwordstore.org/][pass]] as my password manager, which comes with its own functions for Emacs
#+BEGIN_SRC emacs-lisp
(use-package password-store
  :ensure nil
  :load-path "~/.nix-profile/share/emacs/site-lisp/password-store.el"
  :bind
  ("C-c M-p" . password-store-copy))
#+END_SRC

*** Smartparens
Brilliant automatic balancing of pairs. Makes for a really nice experience when typing in any language - programming or not.
Just check out some of the gifs in the project's README.
#+BEGIN_SRC emacs-lisp
(use-package smartparens
  :hook (after-init . smartparens-global-mode)
  :config
  (use-package evil-smartparens
    :ensure t
    :hook
    (smartparens-global-mode . evil-smartparens-mode)))
#+END_SRC

*** all-the-icons
This places little glyphs around to better convey some things where text may be a bit cluttered. That, and it makes things look nice! We’re visual creatures, after-all.
#+BEGIN_SRC emacs-lisp
(use-package all-the-icons
  :config
  (use-package all-the-icons-dired
    :ensure t
    :hook
    (dired-mode . all-the-icons-dired-mode)))
#+END_SRC

*** rainbow-delimeters
Colourize delimiters differently based on their depth. Really helps you not get burried when you’re in deep.
#+BEGIN_SRC emacs-lisp
(use-package rainbow-delimiters
  :hook
  (prog-mode . rainbow-delimiters-mode)
  (yaml-mode . rainbow-delimiters-mode))
#+END_SRC

*** corral
Quickly surround text with delimiters.
#+BEGIN_SRC emacs-lisp
(use-package corral
  :bind
  ("M-9" . corral-parentheses-backward)
  ("M-0" . corral-parentheses-forward)
  ("M-[" . corral-brackets-backward)
  ("M-]" . corral-brackets-forward)
  ("M-{" . corral-braces-backward)
  ("M-}" . corral-braces-forward)
  ("M-'" . corral-single-quotes-backward)
  ("M-\"" . corral-double-quotes-backward))
#+END_SRC

*** expand-region
Select regions by semantic units. Really handy for selecting regions of data - just repeat keypress to expand selection further.
#+BEGIN_SRC emacs-lisp
(use-package expand-region
  :bind ("C-=" . er/expand-region))
#+END_SRC

*** hl-todo
NOTE/TODO/FIXME highlighting in comments
#+BEGIN_SRC emacs-lisp
(use-package hl-todo
  :hook
  (after-init . global-hl-todo-mode)
  (yaml-mode . hl-todo-mode))
#+END_SRC

*** centered-window-mode
Keep buffer text centered
#+BEGIN_SRC emacs-lisp
(use-package centered-window)
#+END_SRC

*** Languages
Configuration for working with various languages
#+BEGIN_SRC emacs-lisp
(use-package nix-mode)
(use-package go-mode)
(use-package json-mode)
(use-package yaml-mode)
(use-package toml-mode)
(use-package rego-mode)

(use-package markdown-mode
  :mode "\\.md\\'"
  :hook
  (markdown-mode . flyspell-mode))

(use-package terraform-mode
  :after lsp-mode
  :hook
  (terraform-mode . company-mode)
  (terraform-mode . (lambda () (lsp)))
  (before-save    . terraform-format-buffer)
  :config
  (lsp-register-client
   ;; TODO: Derive the terraform-ls bin location from PATH
   ;;(make-lsp-client :new-connection (lsp-stdio-connection '((locate-file "terraform-ls" exec-path) "serve"))
   (make-lsp-client :new-connection (lsp-stdio-connection '("/Users/cmacrae/.nix-profile/bin/terraform-ls" "serve"))
                    :major-modes '(terraform-mode)
                    :server-id 'terraform-ls)))

(use-package dockerfile-mode
  :mode "\\Dockerfile\\'")

(use-package web-mode
  :mode "\\.html"
  :custom
  (web-mode-engines-alist '(("go" . "\\.gotmpl\\'"))))

(use-package swift-mode
  :mode "\\.swift"
  :hook (swift-mode . (lambda () (lsp)))
  :config
  (use-package swift-helpful
    :ensure t))
#+END_SRC

Language Server Protocol integration
#+BEGIN_SRC emacs-lisp
(use-package lsp-mode
  :commands (lsp lsp-deferred)
  :hook
  (lsp-mode . lsp-install-save-hooks)
  (nix-mode . lsp-deferred)
  (go-mode  . lsp-deferred)
  (go-mode  . lsp-go-install-save-hooks)

  :custom
  (gc-cons-threshold 100000000)
  (read-process-output-max (* 1024 1024))
  (lsp-before-save-edits t)
  (lsp-imenu-show-container-name nil)
  (lsp-headerline-breadcrumb-enable nil)

  :config
  (defun lsp-install-save-hooks ()
    (add-hook 'before-save-hook #'lsp-format-buffer t t))
  (defun lsp-go-install-save-hooks ()
    (add-hook 'before-save-hook #'lsp-organize-imports t t))

  (add-to-list 'lsp-language-id-configuration '(nix-mode . "nix"))
  (lsp-register-client
   (make-lsp-client :new-connection (lsp-stdio-connection '("rnix-lsp"))
                    :major-modes '(nix-mode)
                    :server-id 'nix)))

  (use-package yasnippet
    :hook (go-mode . yas-minor-mode))

  (use-package lsp-ui
    :commands lsp-ui-mode)

  (use-package lsp-sourcekit
    :after lsp-mode
    :custom
    (lsp-sourcekit-executable "/Applications/Xcode.app/Contents/Developer/Toolchains/XcodeDefault.xctoolchain/usr/bin/sourcekit-lsp"))
#+END_SRC

** Custom functions
Useful functions gathered that don’t quite require an entire package.

*** Sensible beginning of line
Taken from [[http://emacsredux.com/blog/2013/05/22/smarter-navigation-to-the-beginning-of-a-line/][here]], I use this to replace move-beginning-of-line (C-a). It will take your point back to the first column of the line you’re on,
as per the indentation. A second press will then take your point back to the very beginning of the line.
Pressing again will take you back to the indented column.
#+BEGIN_SRC emacs-lisp
(defun cm/sensible-move-beginning-of-line (arg)
  "Move point back to indentation of beginning of line.

      Move point to the first non-whitespace character on this line.
      If point is already there, move to the beginning of the line.
      Effectively toggle between the first non-whitespace character and
      the beginning of the line.

      If ARG is not nil or 1, move forward ARG - 1 lines first.  If
      point reaches the beginning or end of the buffer, stop there."
  (interactive "^p")
  (setq arg (or arg 1))

  ;; Move lines first
  (when (/= arg 1)
    (let ((line-move-visual nil))
      (forward-line (1- arg))))

  (let ((orig-point (point)))
    (back-to-indentation)
    (when (= orig-point (point))
      (move-beginning-of-line 1))))

(global-set-key (kbd "C-a") 'cm/sensible-move-beginning-of-line)
#+END_SRC
